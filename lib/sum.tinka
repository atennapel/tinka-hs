import lib/bool;
import lib/functor;
import lib/eq;

Sum <l> (A B : Type l) : Type l = (b : Bool) ** if b A B;

(+:) = Sum;

Left <l> {A B : Type l} (x : A) : Sum A B = (True, x);
Right <l> {A B : Type l} (x : B) : Sum A B = (False, x);

indSum
  <l k> {A B : Type l}
  (P : Sum A B -> Type k)
  (left : (x : A) -> P (Left x))
  (right : (x : B) -> P (Right x))
  (sum : Sum A B) : P sum
  = indBool (\b. (x : if b A B) -> P (b, x)) left right sum._1 sum._2;

sum
  <l k> {A B : Type l} {R : Type k}
  (left : A -> R)
  (right : B -> R)
  (x : Sum A B) : R
  = indSum <l> <k> (\_. R) left right x;

swapSum <l> {A B : Type l} (x : Sum A B) : Sum B A = sum <l> <l> Right Left x;

mapSum <l> {X A B : Type l} (fn : A -> B) (x : Sum X A) : Sum X B
  = sum <l> <l> Left (\x. Right (fn x)) x;

instance functorSum : {X : Type} -> Functor (Sum X) = \{X}. MkFunctor (mapSum {X});

instance eqSum : {A B : Type} {{_ : Eq A}} {{_ : Eq B}} -> Eq (Sum A B)
  = \{A} {B}. MkEq {Sum A B} (\a b. sum {A} {B} (\x. sum (\y. eq x y) (\_. False) b) (\x. sum (\_. False) (\y. eq x y) b) a);
